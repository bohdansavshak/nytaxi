import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1' apply false
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.bohdansavshak'
version = '0.0.1-SNAPSHOT'

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		sourceCompatibility '21'
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom SpringBootPlugin.BOM_COORDINATES
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'io.projectreactor:reactor-test'
		testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
		testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
	}

	test {
		useJUnitPlatform()

		testLogging {
			events "passed", "skipped", "failed"
		}

		afterSuite { desc, result ->
			if (!desc.parent) {
				println "\nTest summary: " +
						"Total tests: ${result.testCount}, " +
						"Succeeded: ${result.successfulTestCount}, " +
						"Failed: ${result.failedTestCount}, " +
						"Skipped: ${result.skippedTestCount}"
			}
		}
	}
}
